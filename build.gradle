buildscript {
    repositories {
        mavenLocal()
        mavenCentral()

    }
}

plugins {
    id 'java'
    id 'maven'
    id 'signing'

    id 'com.github.ben-manes.versions' version '0.21.0'
    id 'com.github.spotbugs' version '2.0.0'
}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

artifacts {
    archives javadocJar, sourcesJar
}

group 'se.fnord'
version '0.6-SNAPSHOT'

ext {
    jackson = '2.9.9'
    log4j = '2.11.2'

    jsonUnit = '2.7.0'
    junit5 = '5.5.0'
    isTravis = System.getenv('TRAVIS') ? true : false
    isRelease = !isTravis && !version.endsWith("SNAPSHOT")

    ossrhUsername = System.getenv('OSSRH_USERNAME')
    ossrhPassword = System.getenv('OSSRH_PASSWORD')
}


sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    all {
        resolutionStrategy {
            failOnVersionConflict()
            // json-unit -> json-unit-core and junit-jupiter-api depend on different versions
            force 'org.opentest4j:opentest4j:1.2.0'
        }
    }
    spotbugs {
        resolutionStrategy {
            // Internal Slf4j version confusion in Spotbugs
            force 'org.slf4j:slf4j-api:1.8.0-beta4'
        }
    }
}

compileJava {
    options.compilerArgs << "-Xlint:all,-classfile,-processing" << "-Werror"
}

compileTestJava {
    options.compilerArgs << "-Xlint:all,-classfile,-processing" << "-Werror"
}

signing {
    useGpgCmd()
    required { isRelease && gradle.taskGraph.hasTask(":uploadArchives") }
    sign configurations.archives
}

dependencies {
    implementation "org.apache.logging.log4j:log4j-core:${log4j}"
    annotationProcessor "org.apache.logging.log4j:log4j-core:${log4j}"
    testAnnotationProcessor "org.apache.logging.log4j:log4j-core:${log4j}"
    implementation "org.apache.logging.log4j:log4j-api:${log4j}"
    implementation "se.fnord:log4j2-tagged-message:0.3"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit5}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junit5}"
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junit5}")

    testImplementation "com.fasterxml.jackson.core:jackson-core:${jackson}"
    testImplementation "com.fasterxml.jackson.core:jackson-databind:${jackson}"
    testImplementation "net.javacrumbs.json-unit:json-unit:${jsonUnit}"
    testImplementation "net.javacrumbs.json-unit:json-unit-fluent:${jsonUnit}"
}

wrapper {
    gradleVersion = '5.5'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            pom.project {
                name project.name
                packaging 'jar'
                description 'Logstash json event layout for Log4J2'
                url 'https://github.com/gsson/log4j2-logstash-layout'

                scm {
                    url 'scm:git:https://github.com/gsson/log4j2-logstash-layout'
                    connection 'scm:git:git@github.com:/gsson/log4j2-logstash-layout'
                    developerConnection 'scm:git:git@github.com:/gsson/log4j2-logstash-layout'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'gsson'
                        name 'Henrik Gustafsson'
                    }
                }
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
        }
    }
}
